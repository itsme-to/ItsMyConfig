plugins {
    id 'java'
    id 'maven-publish'
    id "io.github.goooler.shadow" version "8.1.7"
}

group = 'to.itsme'
version = '3.1.0'
description = 'ItsMyConfig'
java.sourceCompatibility = JavaVersion.VERSION_1_8
repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = "spigot-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype-snapshots"
        url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
    }
    maven {
        name = "jitpack"
        url = "https://jitpack.io"
    }
    maven {
        name = "protocollib-repo"
        url = "https://repo.dmulloy2.net/repository/public/"
    }
    maven {
        name = "papi-repo"
        url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
    maven {
        name = "devmart-other"
        url = "https://nexuslite.gcnt.net/repos/other/"
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/itsme-to/ItsMyConfig")

            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

def lampVersion = "3.1.9"
def adventureVersion = "4.17.0"
dependencies {
    implementation "com.tcoded:FoliaLib:0.3.4"
    implementation "org.bstats:bstats-bukkit:3.0.2"
    implementation "net.kyori:adventure-platform-bukkit:4.3.3-SNAPSHOT"
    implementation "com.github.Revxrsal.Lamp:common:${lampVersion}"
    implementation "com.github.Revxrsal.Lamp:bukkit:${lampVersion}"
    implementation "net.kyori:adventure-text-minimessage:${adventureVersion}"
    implementation "net.kyori:adventure-text-serializer-gson:${adventureVersion}"

    compileOnly "org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT"
    compileOnly "com.comphenix.protocol:ProtocolLib:5.0.0"
    compileOnly "me.clip:placeholderapi:2.11.1"
}
tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.compilerArgs += ["-parameters"]
}
processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

def shadePath = "to.itsme.itsmyconfig.shade."
shadowJar {
    relocate("net.kyori", "${shadePath}kyori")
    relocate("com.tcoded.folialib", "${shadePath}folialib")
    relocate("com.google.gson", "${shadePath}gson")
    relocate("revxrsal.commands", "${shadePath}lamp")
    relocate("org.bstats", "${shadePath}bstats")
    archiveFileName = "${project.name}-${project.version}.jar"
}
